plugins {
    id 'java-library'
    id 'groovy'
    id 'maven-publish'
    id 'org.jreleaser' version '1.14.0'
}

allprojects {
    group = 'dk.fust.docgen'
    version = '1.5.0'

    plugins.withType(JavaPlugin).whenPluginAdded {
        dependencies {
            implementation 'org.projectlombok:lombok:1.18.34'
            annotationProcessor 'org.projectlombok:lombok:1.18.34'

            implementation 'org.slf4j:slf4j-api:2.0.17'
            testRuntimeOnly 'org.slf4j:slf4j-log4j12:2.0.13'
            testRuntimeOnly 'org.apache.logging.log4j:log4j-api:2.23.1'
            testRuntimeOnly 'org.apache.logging.log4j:log4j-core:2.23.1'

            testImplementation project(':helpers:documentation-generator-test-utilities')
            testImplementation platform('org.spockframework:spock-bom:2.3-groovy-4.0')
            testImplementation 'org.spockframework:spock-core'
            testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
        }

        tasks.withType(Test).configureEach {
            // Using JUnitPlatform for running tests
            useJUnitPlatform()

        }
        java {
            withSourcesJar()
            withJavadocJar()
        }
    }

    plugins.withType(MavenPublishPlugin).whenPluginAdded {
        publishing {
            publications {
                maven(MavenPublication) {
                    groupId = group
                    artifactId = project.name
                    version = version

                    from components.java

                    pom {
                        name = 'Documentation Generator'
                        url = 'https://github.com/patrickfust/documentation-generator'
                        licenses {
                            license {
                                name = 'The Apache License, Version 2.0'
                                url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                            }
                        }
                        developers {
                            developer {
                                id = 'patrickfust'
                                name = 'Patrick Fust'
                                email = 'patrick.fust@gmail.com'
                            }
                        }
                        scm {
                            connection = 'scm:git:git://github.com/patrickfust/documentation-generator.git'
                            developerConnection = 'scm:git:git://github.com/patrickfust/documentation-generator.git'
                            url = 'https://github.com/patrickfust/documentation-generator'
                        }
                    }
                }
            }
        }
    }

    repositories {
        mavenLocal()
        mavenCentral()
    }

}

jreleaser {
    configFile = 'jreleaser.yml'
}
